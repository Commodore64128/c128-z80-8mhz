Name     z80-dot ;
PartNo   00 ;
Date     2024-06-02 ;
Revision 01 ;
Designer Maciej Witkowiak ;
Company  YTM Enterprises ;
Assembly None ;
Location  ;
Device   G16V8 ;

/* *************** INPUT PINS *********************/
PIN 1   = CLK1MHZ	;	/* U12 pin 11, Z80 is active on low phase of the system clock */
PIN 2   = DOTCLK	;	/* 8MHz dot clock from expansion port pin 6 */
PIN 3   = CLKIN	;	/* Z80 socket pin 6, original clock */
PIN 4   = !MREQ	;	/* Z80 pin 19, memory request */
PIN 5   = !IORQ	;	/* Z80 pin 20, I/O request */
/* pins 6, 7, 8 not used but were connected on v1 PCB */
PIN 6   = !BUSRQ	; 	/* bus request from VIC(?) or bus disconnect when 6502 is active */
PIN 7   = !BUSACK	; 	/* Z80 ack for bus request */
PIN 8	= !M1		; 	/* first machine cycle */
PIN 9	= !RFSH	;	/* Z80 pin 28, if active: the MREQ request is memory refresh, ignore */
PIN 11 = !RESET	;	/* Z80 pin 26 */
PIN 12 = WAITLATCH	;	/* input: latched wait from 74'74 pin 6 */
PIN 13 = !WR		;	/* Z80 pin 22, (active one cycle later than MREQ, without delay with IORQ) */
PIN 14 = CLOCKSEL    ;     /* jumper for clock selection: open (internal pullup) = FAST, closed to GND = SLOW */

/* *************** OUTPUT PINS *********************/

PIN 19	= CLKOUT	;	/* Z80 pin 6, clock, 4/8 MHz half of the time */
PIN 18	= WAIT		;	/* Z80 pin 24 */
PIN 17	= WAITTRIGGER ;	/* output: trigger latch to hold WAITLATCH low, 74'74 pin 3 */
PIN 16 = WAITRESET	;	/* output: reset latch, 74'74 pin 1 */

/** Logic Equations **/

/* output 8MHz during CLK1MHZ low phase and no memory request */
/* hold low during CLK1MHZ low phase and memory write request, until next 1MHz clock tick */
/* output mainboard clock CLKIN if CLOCKSEL is grounded */

CLKOUT = (!RESET &  CLOCKSEL & !CLK1MHZ & !MREQ & !WR & !DOTCLK ) 
       # (!RESET &  CLOCKSEL & !CLK1MHZ & !MREQ &  WR & !DOTCLK )
       # (!RESET &  CLOCKSEL & !CLK1MHZ &  MREQ &  WR )
       # (!RESET &  CLOCKSEL & !CLK1MHZ &  MREQ & !WR & !DOTCLK )
       # (!RESET & !CLOCKSEL & CLKIN )
;

/* reset latch when going into VIC phase, keep that line high during Z80 phase */
WAITRESET = RESET # !CLK1MHZ;

/* trigger latch on any address bus request: I/O or memory but not during memory refresh */
WAITTRIGGER = !RESET & !RFSH & (MREQ # IORQ);

/* copy latched wait status to Z80 /WAIT input or keep it high when slow mode is selected */
WAIT = CLOCKSEL & WAITLATCH # !CLOCKSEL;

/*
   7474:

   WAITRESET 1   14 VCC
         VCC 2   13 VCC
 WAITTRIGGER 3   12 GND
         VCC 4   11 GND
             5   10 VCC
   WAITLATCH 6    9
         GND 7    8
*/
